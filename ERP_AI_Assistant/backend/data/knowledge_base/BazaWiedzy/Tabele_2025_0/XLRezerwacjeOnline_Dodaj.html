<HTML>
  <HEAD>
    <META http-equiv="Content-Type" content="text/html; charset=utf-8">
    <META HTTP-EQUIV="Content-Type" CONTENT="text/html; charset=windows-1250">
    <META NAME="Author" CONTENT="Comarch S.A.">
    <LINK rel="stylesheet" type="text/css" href="Style/doc.css">
    <TITLE>Skrypt SQL</TITLE>
  </HEAD>
  <BODY>
<DIV CLASS="TextInfo">
      <P><A name="("></A><PRE>
          <FONT SIZE="2"><I>/* ( */</I><BR>
CREATE TYPE CDN.tParam_XLRezerwacjeOnline_Dodaj AS TABLE (
		GUID UNIQUEIDENTIFIER NOT NULL,
		MagNumer INT NOT NULL,
		TwrNumer INT NOT NULL,
		TpAId INT NOT NULL,
		Ilosc DECIMAL(19, 4) NOT NULL,
		TworzRzwWPrzyszlosc BIT NOT NULL,
		ZrdLp SMALLINT,
		ZrdSubLp SMALLINT,
		TypRzW TINYINT,
		DstNumer INT,
		IloscMag DECIMAL(11, 4)
	);
</FONT>
        </PRE>
      </P>
      <HR>
    </DIV>
<DIV CLASS="TextInfo">
      <P><A name="XLRezerwacjeOnline_Dodaj"></A><PRE>
          <FONT SIZE="2"><I>/* XLRezerwacjeOnline_Dodaj */</I><BR>
CREATE PROCEDURE CDN.XLRezerwacjeOnline_Dodaj
(
	@p_Rezerwacje CDN.tParam_XLRezerwacjeOnline_Dodaj READONLY, -- Lista parametrów tworzonych rezerwacji
	@p_Tryb INT = 0, 
	@p_ZrdNumer INT = NULL,
	@p_PominTworzenieTblWynikowej BIT = 0,
	@p_Status INT OUTPUT 

	/*
	@p_Rezerwacje - Lista parametrów tworzonych rezerwacji

	@p_Tryb - Tryb tworzenia rezerwacji:
		0 - zmiany w bazie wprowadzane są jedynie w przypadku, gdy uda się wykonać wszystkie żądane rezerwacje, w ilościach takich samych jak żądane. Niepowodzenie przy tworzeniu pojedynczej rezerwacji powinno przerywać proces tworzenia rezerwacji.
		1 - niepowodzenie przy tworzeniu pojedynczej rezerwacji nie ma wpływu na pozostałe rezerwacje. W przypadku, gdy dostępna jest tylko część żądanej ilości, dla danej rezerwacji, powinna zostać utworzona rezerwacja na mniejszą ilość (maksymalnie dostępną).
		2 - tryb wewnętrzny, z metody XLRezerwacjeOnline_Modyfikuj - podobnie jak tryb=0, dodatkowo nie zwraca tabeli wynikowej

	@p_ZrdNumer - GidNumer dokumentu jeśli wołane dla XL'a

	@p_PominTworzenieTblWynikowej:
		0 - procedura sama tworzy tymczasową tabele wynikową i zwracą ja na koniec
		1 - tabela tymczasowa jest dodana wcześniej poza procedurą

	@p_Status - Status tworzenia rezerwacji:
		1 – tworzenie wszystkich żądanych rezerwacji powiodło się,
		2 – odpowiedź zwracana w przypadku, gdy tylko część żądanych rezerwacji została utworzona na ilość wymaganą lub mniejszą dostępną (dotyczy @p_Tryb = 1),
		3 – żadna z żądanych rezerwacji nie została utworzona 
	*/
)
AS
BEGIN
	SET NOCOUNT ON;

	SET @p_Status = 3

	DECLARE @bWystapilBlad bit;

	DECLARE @FirmaID INT
	EXEC @FirmaID = CDN.GIDFirma
	DECLARE @FrsId INT

	DECLARE @DataAkt INT; SET @DataAkt = DATEDIFF(d,'18001228',GETDATE());
	DECLARE @DataNieokreslona INT; SET @DataNieokreslona=93890;

	DECLARE @PriorytetRzw INT;
	SELECT @PriorytetRzw=SLW_WartoscL1 
		FROM CDN.Slowniki
		JOIN CDN.SlownikiStruktura on SLS_Id=SLW_SLSId
		WHERE SLS_Predefiniowany=55 AND SLW_Domyslny=1

	DECLARE @OpeNumer INT; SET @OpeNumer = 0;
	DECLARE @KntNumer INT; 

	IF @p_ZrdNumer is not null and @p_ZrdNumer&lt;&gt;0 BEGIN
		SELECT @OpeNumer=MaN_OpeNumer, @FrsId=MaN_FrsID, @KntNumer=MaN_KntNumer
			FROM CDN.MagNag 
			WHERE MaN_GidNumer=@p_ZrdNumer
	END ELSE BEGIN
		SELECT @OpeNumer=Ope_GidNumer, @FrsId=Ope_FrSId
			FROM CDN.OpeKarty 
			WHERE Ope_Ident='WMSADMIN'	
	END

	IF @p_PominTworzenieTblWynikowej = 0 BEGIN		
		CREATE TABLE #Wynik_XLRezerwacjeOnline_Dodaj
		(
		   GUID UNIQUEIDENTIFIER,
		   TwrNumer INT,
		   TpAId INT,
		   Ilosc DECIMAL(19,4),
		   MagNumer INT,
		   Status INT,
		   OpisBledu VARCHAR(MAX),
		   RzwWPrzyszlosc BIT,
		   ZrdLp SMALLINT,
		   ZrdSubLp SMALLINT,
		   ZrdSubLp2 SMALLINT
		)
	END ELSE BEGIN
		IF OBJECT_ID('tempdb..#Wynik_XLRezerwacjeOnline_Dodaj') IS NULL BEGIN
			RAISERROR('Brak tabeli #Wynik_XLRezerwacjeOnline_Dodaj.',16,1);
			SET NOCOUNT OFF;
			RETURN
		END
	END

	DECLARE @DodaneRezerwacje TABLE(WPrzyszlosc BIT, Id INT, GUID UNIQUEIDENTIFIER, Ilosc DECIMAL(19,4));

	CREATE TABLE #temp_XLRezerwacjeOnline_Dodaj_PobierzZasoby
	(
	   DstNumer INT,
	   Ilosc DECIMAL(28, 4)
	)
	DECLARE @WynikINT INT

	DECLARE @c_GUID UNIQUEIDENTIFIER;
	DECLARE @c_MagNumer INT;
	DECLARE @c_TwrNumer INT;
	DECLARE @c_TpAId INT;
	DECLARE @c_Ilosc DECIMAL(19, 4);
	DECLARE @c_TworzRzwWPrzyszlosc BIT;
	DECLARE @c_ZrdLp SMALLINT;
	DECLARE @c_ZrdSubLp SMALLINT;
	DECLARE @c_TypRzW TINYINT;
	DECLARE @c_DstNumer INT;
	DECLARE @c_IloscMag DECIMAL(11, 4);

	DECLARE @_TRANCOUNT int;
	IF @p_Tryb &lt;&gt; 1 BEGIN
		SELECT @_TRANCOUNT=@@TRANCOUNT; 
		IF @_TRANCOUNT&gt;0 SAVE TRANSACTION TRAN_XLRezerwacjeOnline_Dodaj;
		ELSE BEGIN TRANSACTION TRAN_XLRezerwacjeOnline_Dodaj;
	END

	DECLARE C1 CURSOR FAST_FORWARD FOR
		SELECT GUID, MagNumer, TwrNumer, TpAId, Ilosc, TworzRzwWPrzyszlosc, IsNull(ZrdLp,0), IsNull(ZrdSubLp,0), IsNull(TypRzW,1), IsNull(DstNumer,0), IsNull(IloscMag,0)
		FROM @p_Rezerwacje
		JOIN CDN.Magazyny ON MAG_GIDNumer=MagNumer
		JOIN CDN.TwrKarty ON Twr_GIDNumer=TwrNumer
		JOIN CDN.TwrPartie ON TPa_Id=TpAId

    OPEN C1;
    FETCH NEXT FROM C1 INTO @c_GUID, @c_MagNumer, @c_TwrNumer, @c_TpAId, @c_Ilosc, @c_TworzRzwWPrzyszlosc, @c_ZrdLp, @c_ZrdSubLp, @c_TypRzW, @c_DstNumer, @c_IloscMag
    WHILE @@fetch_status = 0
    BEGIN
		IF @c_DstNumer = 0 BEGIN
			DELETE FROM #temp_XLRezerwacjeOnline_Dodaj_PobierzZasoby

			DECLARE @TrybDla_PobierzZasoby BIT
			SELECT @TrybDla_PobierzZasoby = CASE WHEN @c_TworzRzwWPrzyszlosc=1 
											THEN 1 										
											ELSE CASE WHEN @p_Tryb=1 
												THEN 1 
												ELSE 0
												END
											END
			
			DECLARE @RezZrdTyp INT
			DECLARE @RezZrdNumer INT
			SELECT @RezZrdTyp=0, @RezZrdNumer=0
			IF @p_ZrdNumer is not null and @p_ZrdNumer&lt;&gt;0 BEGIN
				SELECT @RezZrdTyp=MaS_ZrdTyp, @RezZrdNumer=MaS_ZrdNumer FROM cdn.MagSElem WHERE MaS_GIDTyp=1605 AND MaS_GIDNumer=@p_ZrdNumer AND MaS_GIDLp=@c_ZrdLp AND MaS_SubGIDLp=@c_ZrdSubLp AND MaS_ZrdTyp in (2576,2592)
			END 

			INSERT INTO #temp_XLRezerwacjeOnline_Dodaj_PobierzZasoby
			EXEC @WynikINT = CDN.XLRezerwacjeOnline_PobierzZasoby 
								@p_Tryb = @TrybDla_PobierzZasoby,
								@p_FrsId = @FrsId,
								@p_TwrNumer = @c_TwrNumer,
								@p_Ilosc = @c_Ilosc,
								@p_MagNumer = @c_MagNumer, 
								@p_TPaId = @c_TpAId,
								@p_RezZrdTyp = @RezZrdTyp,
								@p_RezZrdNumer = @RezZrdNumer

			DECLARE @ZasobDstNumer INT;
			DECLARE @ZasobIlosc DECIMAL(19, 4);
			DECLARE @ZasobCCHNumer INT;
			DECLARE @ZasobCCHCecha VARCHAR(255);

			DECLARE @Rez_Ilosc DECIMAL(19, 4);
			DECLARE @IloscPozostala DECIMAL(19, 4);
			SET @IloscPozostala = @c_Ilosc;

			DECLARE @Iteracja SMALLINT;
			SET @Iteracja = 0;
			DECLARE @NoweSubLp SMALLINT;
			SET @NoweSubLp = 0;


			WHILE @IloscPozostala&gt;0 AND EXISTS(SELECT * FROM #temp_XLRezerwacjeOnline_Dodaj_PobierzZasoby)
			BEGIN
				SET @Iteracja = @Iteracja + 1;				

				SELECT TOP(1) @ZasobDstNumer=DstNumer, @ZasobIlosc=Ilosc, @ZasobCCHNumer=Dst_CCKNumer, @ZasobCCHCecha=Dst_Cecha
				FROM #temp_XLRezerwacjeOnline_Dodaj_PobierzZasoby
				JOIN CDN.Dostawy ON Dst_GIDNumer=DstNumer

				IF @ZasobIlosc&gt;=@IloscPozostala BEGIN 
					SET @Rez_Ilosc = @IloscPozostala
					SET @IloscPozostala = 0
				END ELSE BEGIN
					SET @Rez_Ilosc = @ZasobIlosc
					SET @IloscPozostala = @IloscPozostala - @ZasobIlosc
				END

				DELETE TOP(1) from #temp_XLRezerwacjeOnline_Dodaj_PobierzZasoby

				BEGIN TRY
						IF @Iteracja&gt;1 AND @p_ZrdNumer IS NOT NULL AND @p_ZrdNumer&lt;&gt;0 BEGIN  
							EXEC @WynikINT = CDN.XLRezerwacjeOnline_PodzielSubElem
												@p_ZrdNumer=@p_ZrdNumer,
												@p_ZrdLp=@c_ZrdLp,
												@p_ZrdSubLp=@c_ZrdSubLp,
												@p_IloscDoWydzielenia=@Rez_Ilosc,
												@p_NoweSubLp=@NoweSubLp OUTPUT	
							SET @c_GUID = NEWID()							
						END ELSE BEGIN
							SET @NoweSubLp=@c_ZrdSubLp
						END
						INSERT INTO CDN.Rezerwacje
						   (Rez_GIDTyp
						   ,Rez_GIDFirma
						   ,Rez_GIDLp
						   ,Rez_TwrTyp
						   ,Rez_TwrFirma
						   ,Rez_TwrNumer
						   ,Rez_TwrLp
						   ,Rez_KntTyp
						   ,Rez_KntFirma
						   ,Rez_KntNumer
						   ,Rez_KntLp
						   ,Rez_ZrdTyp
						   ,Rez_ZrdFirma
						   ,Rez_ZrdNumer
						   ,Rez_ZrdLp
						   ,Rez_ZrdSubLp
						   ,Rez_OpeTyp
						   ,Rez_OpeFirma
						   ,Rez_OpeNumer
						   ,Rez_OpeLp
						   ,Rez_MagTyp
						   ,Rez_MagFirma
						   ,Rez_MagNumer
						   ,Rez_MagLp
						   ,Rez_DstTyp
						   ,Rez_DstFirma
						   ,Rez_DstNumer
						   ,Rez_DstLp
						   ,Rez_Ilosc
						   ,Rez_Zrealizowano
						   ,Rez_IloscMag
						   ,Rez_IloscImp
						   ,Rez_IloscSSC
						   ,Rez_IloscSAD
						   ,Rez_TStamp
						   ,Rez_DataRealizacji
						   ,Rez_DataWaznosci
						   ,Rez_DataAktywacji
						   ,Rez_Aktywna
						   ,Rez_Zrodlo
						   ,Rez_DataPotwDst
						   ,Rez_FrsID
						   ,Rez_Typ
						   ,Rez_Priorytet
						   ,Rez_DataRezerwacji
						   ,Rez_BsSTwrNumer
						   ,Rez_BsNID
						   ,Rez_BsSRodzaj
						   ,Rez_PTZID
						   ,Rez_CCHNumer
						   ,Rez_Cecha
						   ,Rez_Opis
						   ,Rez_GUID
						   )
						OUTPUT 0, inserted.Rez_GIDNumer, inserted.Rez_GUID, inserted.Rez_Ilosc INTO @DodaneRezerwacje(WPrzyszlosc,Id,GUID,Ilosc) 
						VALUES
						   (2576		--Rez_GIDTyp
						   ,@FirmaID	--Rez_GIDFirma
						   ,0			--Rez_GIDLp
						   ,16			--Rez_TwrTyp
						   ,@FirmaID	--Rez_TwrFirma
						   ,@c_TwrNumer	--Rez_TwrNumer
						   ,0			--Rez_TwrLp
						   ,CASE WHEN @KntNumer IS NULL THEN 0 ELSE 32 END			--Rez_KntTyp
						   ,CASE WHEN @KntNumer IS NULL THEN 0 ELSE @FirmaID END    --Rez_KntFirma
						   ,CASE WHEN @KntNumer IS NULL THEN 0 ELSE @KntNumer END	--Rez_KntNumer
						   ,0														--Rez_KntLp
						   ,CASE WHEN @p_ZrdNumer IS NULL OR @p_ZrdNumer=0 THEN -1605 ELSE 1605 END --Rez_ZrdTyp
						   ,@FirmaID				--Rez_ZrdFirma
						   ,IsNull(@p_ZrdNumer,0)	--Rez_ZrdNumer
						   ,@c_ZrdLp				--Rez_ZrdLp
						   ,@NoweSubLp				--Rez_ZrdSubLp
						   ,128			--Rez_OpeTyp
						   ,@FirmaID	--Rez_OpeFirma
						   ,IsNull(@OpeNumer,0)	--Rez_OpeNumer 
						   ,0			--Rez_OpeLp
						   ,208			--Rez_MagTyp
						   ,@FirmaID	--Rez_MagFirma
						   ,@c_MagNumer	--Rez_MagNumer
						   ,0			--Rez_MagLp
						   ,CASE WHEN @ZasobDstNumer=0 THEN 0 ELSE 160 END --Rez_DstTyp
						   ,CASE WHEN @ZasobDstNumer=0 THEN 0 ELSE @FirmaID END--Rez_DstFirma 
						   ,@ZasobDstNumer
						   ,0--Rez_DstLp
						   ,@Rez_Ilosc--Rez_Ilosc
						   ,0--Rez_Zrealizowano
						   ,CASE WHEN @c_IloscMag&lt;=@Rez_Ilosc THEN @c_IloscMag ELSE 0 END--Rez_IloscMag
						   ,0--Rez_IloscImp
						   ,0--Rez_IloscSSC
						   ,0--Rez_IloscSAD
						   ,DATEDIFF(s,'19900101',GETDATE())	--Rez_TStamp
						   ,@DataNieokreslona					--Rez_DataRealizacji
						   ,@DataNieokreslona					--Rez_DataWaznosci
						   ,@DataAkt	--Rez_DataAktywacji
						   ,1			--Rez_Aktywna
						   ,16			--Rez_Zrodlo
						   ,@DataAkt	--Rez_DataPotwDst
						   ,IsNull(@FrsId,0)	--Rez_FrsID
						   ,@c_TypRzW	--Rez_Typ
						   ,@PriorytetRzw--Rez_Priorytet
						   ,DATEDIFF(s,'19900101',GETDATE())--Rez_DataRezerwacji
						   ,0			--Rez_BsSTwrNumer
						   ,0			--Rez_BsNID
						   ,0			--Rez_BsSRodzaj
						   ,0			--Rez_PTZID
						   ,@ZasobCCHNumer	--Rez_CCHNumer
						   ,@ZasobCCHCecha	--Rez_Cecha
						   ,''			--Rez_Opis
						   ,CAST(@c_GUID as varchar(36))	--Rez_GUID
						   )

						IF @p_ZrdNumer IS NOT NULL AND @p_ZrdNumer&lt;&gt;0 BEGIN  
							INSERT INTO #Wynik_XLRezerwacjeOnline_Dodaj
							SELECT @c_GUID, @c_TwrNumer, @c_TpAId, @Rez_Ilosc, @c_MagNumer, 1 as Status, '' as OpisBledu, 0 as RzwWPrzyszlosc, @c_ZrdLp as ZrdLp, @NoweSubLp as ZrdSubLp, @c_ZrdSubLp as ZrdSubLp2
						END
				END TRY
				BEGIN CATCH
					IF @p_Tryb = 1
						INSERT INTO #Wynik_XLRezerwacjeOnline_Dodaj
						SELECT @c_GUID as GUID, @c_TwrNumer as TwrNumer, @c_TpAId as TpAId, @Rez_Ilosc as Ilosc, @c_MagNumer as MagNumer, 0 as Status, CONCAT(
						'ErrorProcedure: ',ERROR_PROCEDURE(),
						'; ErrorLine: ',ERROR_LINE(),  
						'; ErrorMessage: ',ERROR_MESSAGE()) as OpisBledu,
						0 as RzwWPrzyszlosc, 0 as ZrdLp, 0 as ZrdSubLp, 0 as ZrdSubLp2;
					ELSE
						SELECT @c_GUID as GUID, @c_TwrNumer as TwrNumer, @c_TpAId as TpAId, @Rez_Ilosc as Ilosc, @c_MagNumer as MagNumer, 0 as Status, CONCAT(
						'ErrorProcedure: ',ERROR_PROCEDURE(),
						'; ErrorLine: ',ERROR_LINE(),  
						'; ErrorMessage: ',ERROR_MESSAGE()) as OpisBledu,
						0 as RzwWPrzyszlosc, 0 as ZrdLp, 0 as ZrdSubLp, 0 as ZrdSubLp2;

					SET @bWystapilBlad = 1

				END CATCH					

				IF @bWystapilBlad = 1  BEGIN
					IF @p_Tryb &lt;&gt; 1 BEGIN
						IF XACT_STATE()&lt;&gt;0 ROLLBACK TRANSACTION TRAN_XLRezerwacjeOnline_Dodaj
						CLOSE C1;
						DEALLOCATE C1;
						SET NOCOUNT OFF;
						RETURN
					END
				END
			END 
			IF @IloscPozostala=0 BEGIN
				IF @p_ZrdNumer IS NULL OR @p_ZrdNumer=0 BEGIN
					INSERT INTO #Wynik_XLRezerwacjeOnline_Dodaj
					SELECT @c_GUID, @c_TwrNumer, @c_TpAId, @c_Ilosc, @c_MagNumer, 1 as Status, '' as OpisBledu, 0 as RzwWPrzyszlosc, 0 as ZrdLp, 0 as ZrdSubLp, 0 as ZrdSubLp2;
				END
			END ELSE BEGIN
				IF @c_TworzRzwWPrzyszlosc=1 BEGIN
					IF @p_ZrdNumer IS NOT NULL AND @p_ZrdNumer&lt;&gt;0 AND EXISTS(SELECT * FROM CDN.Rezerwacje WHERE Rez_ZrdTyp=1605 AND Rez_ZrdNumer=@p_ZrdNumer AND Rez_ZrdLp=@c_ZrdLp AND Rez_ZrdSubLp=@c_ZrdSubLp) BEGIN
						EXEC @WynikINT = CDN.XLRezerwacjeOnline_PodzielSubElem
											@p_ZrdNumer=@p_ZrdNumer,
											@p_ZrdLp=@c_ZrdLp,
											@p_ZrdSubLp=@c_ZrdSubLp,
											@p_IloscDoWydzielenia=@IloscPozostala,
											@p_NoweSubLp=@NoweSubLp OUTPUT	
						SET @c_GUID = NEWID()						
					END ELSE BEGIN
						SET @NoweSubLp=@c_ZrdSubLp
					END

					INSERT INTO CDN.RezerwacjeWPrzyszlosc
								(RezP_GUID
							   ,RezP_TwrTyp
							   ,RezP_TwrFirma
							   ,RezP_TwrNumer
							   ,RezP_TwrLp
							   ,RezP_ZrdTyp
							   ,RezP_ZrdFirma
							   ,RezP_ZrdNumer
							   ,RezP_ZrdLp
							   ,RezP_ZrdSubLp
							   ,RezP_MagTyp
							   ,RezP_MagFirma
							   ,RezP_MagNumer
							   ,RezP_MagLp
							   ,RezP_TPaId
							   ,RezP_Ilosc
							   ,RezP_TStamp)
					OUTPUT 1, inserted.RezP_Id, inserted.RezP_GUID, inserted.RezP_Ilosc INTO @DodaneRezerwacje(WPrzyszlosc,Id,GUID,Ilosc) 
					VALUES
								(CAST(@c_GUID as varchar(36))	--RezP_GUID
								,16			--RezP_TwrTyp
								,@FirmaID	--RezP_TwrFirma
								,@c_TwrNumer	--RezP_TwrNumer
								,0			--RezP_TwrLp
								,CASE WHEN @p_ZrdNumer IS NULL OR @p_ZrdNumer=0 THEN -1605 ELSE 1605 END--RezP_ZrdTyp
								,@FirmaID				--RezP_ZrdFirma
								,IsNull(@p_ZrdNumer,0)	--RezP_ZrdNumer
								,@c_ZrdLp				--RezP_ZrdLp
								,@NoweSubLp				--RezP_ZrdSubLp
								,208				--RezP_MagTyp
								,@FirmaID		--RezP_MagFirma
								,@c_MagNumer		--RezP_MagNumer
								,0				--RezP_MagLp
								,@c_TpAId		--RezP_TPaId
								,@IloscPozostala--RezP_Ilosc
								,DATEDIFF(s,'19900101',GETDATE())--RezP_TStamp
								)

					IF @p_ZrdNumer IS NULL OR @p_ZrdNumer=0 BEGIN
						INSERT INTO #Wynik_XLRezerwacjeOnline_Dodaj
						SELECT @c_GUID, @c_TwrNumer, @c_TpAId, @c_Ilosc, @c_MagNumer, 1 as Status, '' as OpisBledu, 0 as RzwWPrzyszlosc, 0 as ZrdLp, 0 as ZrdSubLp, 0 as ZrdSubLp2;
					END ELSE BEGIN
						INSERT INTO #Wynik_XLRezerwacjeOnline_Dodaj
						SELECT @c_GUID, @c_TwrNumer, @c_TpAId, @IloscPozostala, @c_MagNumer, 1 as Status, '' as OpisBledu, 1 as RzwWPrzyszlosc, @c_ZrdLp as ZrdLp, @NoweSubLp as ZrdSubLp, @c_ZrdSubLp as ZrdSubLp2
					END
					
				END ELSE BEGIN
					IF @p_ZrdNumer IS NULL OR @p_ZrdNumer=0 BEGIN
						INSERT INTO #Wynik_XLRezerwacjeOnline_Dodaj
						SELECT @c_GUID, @c_TwrNumer, @c_TpAId, @c_Ilosc-@IloscPozostala, @c_MagNumer, case when @c_Ilosc-@IloscPozostala=0 then 3 else 2 end as Status, '' as OpisBledu, 0 as RzwWPrzyszlosc, 0 as ZrdLp, 0 as ZrdSubLp, 0 as ZrdSubLp2;
					END
				END	
			END
		END ELSE BEGIN
			BEGIN TRY
				SELECT @ZasobDstNumer=Dst_GidNumer, @ZasobCCHNumer=Dst_CCKNumer, @ZasobCCHCecha=Dst_Cecha
				FROM CDN.Dostawy 
				WHERE Dst_GIDNumer=@c_DstNumer

				INSERT INTO CDN.Rezerwacje
					(Rez_GIDTyp
					,Rez_GIDFirma
					,Rez_GIDLp
					,Rez_TwrTyp
					,Rez_TwrFirma
					,Rez_TwrNumer
					,Rez_TwrLp
					,Rez_KntTyp
					,Rez_KntFirma
					,Rez_KntNumer
					,Rez_KntLp
					,Rez_ZrdTyp
					,Rez_ZrdFirma
					,Rez_ZrdNumer
					,Rez_ZrdLp
					,Rez_ZrdSubLp
					,Rez_OpeTyp
					,Rez_OpeFirma
					,Rez_OpeNumer
					,Rez_OpeLp
					,Rez_MagTyp
					,Rez_MagFirma
					,Rez_MagNumer
					,Rez_MagLp
					,Rez_DstTyp
					,Rez_DstFirma
					,Rez_DstNumer
					,Rez_DstLp
					,Rez_Ilosc
					,Rez_Zrealizowano
					,Rez_IloscMag
					,Rez_IloscImp
					,Rez_IloscSSC
					,Rez_IloscSAD
					,Rez_TStamp
					,Rez_DataRealizacji
					,Rez_DataWaznosci
					,Rez_DataAktywacji
					,Rez_Aktywna
					,Rez_Zrodlo
					,Rez_DataPotwDst
					,Rez_FrsID
					,Rez_Typ
					,Rez_Priorytet
					,Rez_DataRezerwacji
					,Rez_BsSTwrNumer
					,Rez_BsNID
					,Rez_BsSRodzaj
					,Rez_PTZID
					,Rez_CCHNumer
					,Rez_Cecha
					,Rez_Opis
					,Rez_GUID
					)
				OUTPUT 0, inserted.Rez_GIDNumer, inserted.Rez_GUID, inserted.Rez_Ilosc INTO @DodaneRezerwacje(WPrzyszlosc,Id,GUID,Ilosc) 
				VALUES
					(2576		--Rez_GIDTyp
					,@FirmaID	--Rez_GIDFirma
					,0			--Rez_GIDLp
					,16			--Rez_TwrTyp
					,@FirmaID	--Rez_TwrFirma
					,@c_TwrNumer	--Rez_TwrNumer
					,0			--Rez_TwrLp
					,CASE WHEN @KntNumer IS NULL THEN 0 ELSE 32 END			--Rez_KntTyp
					,CASE WHEN @KntNumer IS NULL THEN 0 ELSE @FirmaID END   --Rez_KntFirma
					,CASE WHEN @KntNumer IS NULL THEN 0 ELSE @KntNumer END	--Rez_KntNumer
					,0														--Rez_KntLp
					,CASE WHEN @p_ZrdNumer IS NULL OR @p_ZrdNumer=0 THEN -1605 ELSE 1605 END --Rez_ZrdTyp
					,@FirmaID				--Rez_ZrdFirma
					,IsNull(@p_ZrdNumer,0)	--Rez_ZrdNumer
					,@c_ZrdLp				--Rez_ZrdLp
					,@c_ZrdSubLp			--Rez_ZrdSubLp
					,128			--Rez_OpeTyp
					,@FirmaID	--Rez_OpeFirma
					,IsNull(@OpeNumer,0)	--Rez_OpeNumer 
					,0			--Rez_OpeLp
					,208			--Rez_MagTyp
					,@FirmaID	--Rez_MagFirma
					,@c_MagNumer	--Rez_MagNumer
					,0			--Rez_MagLp
					,CASE WHEN @ZasobDstNumer=0 THEN 0 ELSE 160 END --Rez_DstTyp
					,CASE WHEN @ZasobDstNumer=0 THEN 0 ELSE @FirmaID END--Rez_DstFirma 
					,@ZasobDstNumer
					,0--Rez_DstLp
					,@c_Ilosc--Rez_Ilosc
					,0--Rez_Zrealizowano
					,CASE WHEN @c_IloscMag&lt;=@c_Ilosc THEN @c_IloscMag ELSE 0 END--Rez_IloscMag
					,0--Rez_IloscImp
					,0--Rez_IloscSSC
					,0--Rez_IloscSAD
					,DATEDIFF(s,'19900101',GETDATE())	--Rez_TStamp
					,@DataNieokreslona					--Rez_DataRealizacji
					,@DataNieokreslona					--Rez_DataWaznosci
					,@DataAkt	--Rez_DataAktywacji
					,1			--Rez_Aktywna
					,16			--Rez_Zrodlo
					,@DataAkt	--Rez_DataPotwDst
					,IsNull(@FrsId,0)	--Rez_FrsID
					,@c_TypRzW	--Rez_Typ
					,@PriorytetRzw--Rez_Priorytet
					,DATEDIFF(s,'19900101',GETDATE())--Rez_DataRezerwacji
					,0			--Rez_BsSTwrNumer
					,0			--Rez_BsNID
					,0			--Rez_BsSRodzaj
					,0			--Rez_PTZID
					,@ZasobCCHNumer	--Rez_CCHNumer
					,@ZasobCCHCecha	--Rez_Cecha
					,''			--Rez_Opis
					,CAST(@c_GUID as varchar(36))	--Rez_GUID
					)

				INSERT INTO #Wynik_XLRezerwacjeOnline_Dodaj
				SELECT @c_GUID, @c_TwrNumer, @c_TpAId, @c_Ilosc, @c_MagNumer, 1 as Status, '' as OpisBledu, 0 as RzwWPrzyszlosc, @c_ZrdLp as ZrdLp, @c_ZrdSubLp as ZrdSubLp, @c_ZrdSubLp as ZrdSubLp2;

			END TRY

			BEGIN CATCH
				IF @p_Tryb = 1 
					INSERT INTO #Wynik_XLRezerwacjeOnline_Dodaj
					SELECT @c_GUID as GUID, @c_TwrNumer as TwrNumer, @c_TpAId as TpAId, @c_Ilosc as Ilosc, @c_MagNumer as MagNumer, 0 as Status, CONCAT(
					'ErrorProcedure: ',ERROR_PROCEDURE() ,		
					'; ErrorLine: ',ERROR_LINE(),  
					'; ErrorMessage: ',ERROR_MESSAGE()) as OpisBledu,
					0 as RzwWPrzyszlosc, 0 as ZrdLp, 0 as ZrdSubLp, 0 as ZrdSubLp2;
				ELSE
					SELECT @c_GUID as GUID, @c_TwrNumer as TwrNumer, @c_TpAId as TpAId, @c_Ilosc as Ilosc, @c_MagNumer as MagNumer, 0 as Status, CONCAT(
					'ErrorProcedure: ',ERROR_PROCEDURE() ,		
					'; ErrorLine: ',ERROR_LINE(),  
					'; ErrorMessage: ',ERROR_MESSAGE()) as OpisBledu,
					0 as RzwWPrzyszlosc, 0 as ZrdLp, 0 as ZrdSubLp, 0 as ZrdSubLp2;

				SET @bWystapilBlad = 1
			END CATCH					

			IF @bWystapilBlad = 1  BEGIN

				IF @p_Tryb &lt;&gt; 1 BEGIN
					IF XACT_STATE()&lt;&gt;0 ROLLBACK TRANSACTION TRAN_XLRezerwacjeOnline_Dodaj
					CLOSE C1;
					DEALLOCATE C1;
					SET NOCOUNT OFF;
					RETURN
				END
			END
		END

        FETCH NEXT FROM C1 INTO  @c_GUID, @c_MagNumer, @c_TwrNumer, @c_TpAId, @c_Ilosc, @c_TworzRzwWPrzyszlosc, @c_ZrdLp, @c_ZrdSubLp, @c_TypRzW, @c_DstNumer, @c_IloscMag
    END
    CLOSE C1;
    DEALLOCATE C1;

	IF @p_Tryb &lt;&gt; 1 BEGIN
		IF EXISTS(SELECT *
					FROM @p_Rezerwacje I
					LEFT JOIN #Wynik_XLRezerwacjeOnline_Dodaj O ON I.GUID=O.GUID
					WHERE O.GUID IS NULL OR Status&lt;&gt;1
					)					
			SET @p_Status = 3				
		ELSE
		begin 
			SET @p_Status = 1
			end
	END ELSE BEGIN
		IF EXISTS(SELECT * FROM #Wynik_XLRezerwacjeOnline_Dodaj WHERE Ilosc&lt;&gt;0) BEGIN
			IF EXISTS(SELECT *
						FROM @p_Rezerwacje I
						LEFT JOIN #Wynik_XLRezerwacjeOnline_Dodaj O ON I.GUID=O.GUID
						WHERE O.GUID IS NULL OR Status&lt;&gt;1
					)			
				SET @p_Status = 2
			ELSE
				SET @p_Status = 1
			END
		ELSE
			SET @p_Status = 3
	END	

	INSERT INTO CDN.RezMagazyny (ReM_RezTyp,ReM_RezFirma,ReM_RezNumer,ReM_RezLp,ReM_MagTyp,ReM_MagFirma,ReM_MagNumer,ReM_MagLp,ReM_ZrdTyp,ReM_ZrdFirma,ReM_ZrdNumer,ReM_ZrdLp,ReM_ZrdSubLp)
	SELECT Rez_GIDTyp,Rez_GIDFirma,Rez_GIDNumer, 1, Rez_MagTyp, Rez_MagFirma, Rez_MagNumer, Rez_MagLp, Rez_ZrdTyp, Rez_ZrdFirma, Rez_ZrdNumer, Rez_ZrdLp, Rez_ZrdSubLp
	FROM CDN.Rezerwacje WHERE Rez_GIDNumer IN (SELECT ID FROM @DodaneRezerwacje WHERE WPrzyszlosc=0)
	AND NOT EXISTS(SELECT * FROM CDN.RezMagazyny WHERE ReM_RezTyp=Rez_GIDTyp AND ReM_RezNumer=Rez_GIDNumer AND ReM_RezLp=1)

	DECLARE @p_RezerwacjeWynikowe CDN.tParam_XLRezerwacjeOnline_AktualizujTypRezerwacjiZrodlowych;
	INSERT INTO @p_RezerwacjeWynikowe (GUID, Ilosc, RzwWPrzyszlosc, MasLp, MasSubLp, MasSubLp2)
	SELECT GUID, Ilosc, RzwWPrzyszlosc, ZrdLp, ZrdSubLp, ZrdSubLp2
	FROM #Wynik_XLRezerwacjeOnline_Dodaj

	BEGIN TRY
		EXEC CDN.XLRezerwacjeOnline_AktualizujTypRezerwacjiZrodlowych
			@p_Rezerwacje = @p_RezerwacjeWynikowe,
			@p_MasNumer = @p_ZrdNumer,
			@p_Tryb = @p_Tryb
	END TRY
	BEGIN CATCH
		DECLARE @ErrorMessage NVARCHAR(MAX);
		SET @ErrorMessage = CONCAT('Błąd aktualizacji typu rezerwacji źrodłowej. ','ErrorProcedure: ',ERROR_PROCEDURE(),'; ErrorLine: ',ERROR_LINE(),'; ErrorMessage: ',ERROR_MESSAGE())
		RAISERROR(@ErrorMessage,16,1);

		SET @bWystapilBlad = 1
	END CATCH					

	IF @p_Tryb &lt;&gt; 1 BEGIN
		IF @bWystapilBlad = 1  BEGIN		
			IF XACT_STATE()&lt;&gt;0 ROLLBACK TRANSACTION TRAN_XLRezerwacjeOnline_Dodaj
			SET NOCOUNT OFF;
			RETURN
		END ELSE BEGIN
			IF @_TRANCOUNT=0 COMMIT TRANSACTION TRAN_XLRezerwacjeOnline_Dodaj;
		END
	END
	IF @p_PominTworzenieTblWynikowej = 0 BEGIN
		IF @p_Tryb &lt;&gt; 2 
			SELECT GUID, TwrNumer, TpAId, Ilosc, MagNumer, Status, OpisBledu, RzwWPrzyszlosc, ZrdLp, ZrdSubLp FROM #Wynik_XLRezerwacjeOnline_Dodaj
		ELSE BEGIN
			IF OBJECT_ID('tempdb..#Wynik_XLRezerwacjeOnline_Dodaj_DlaModyf') IS NULL BEGIN
				RAISERROR('Brak tabeli #Wynik_XLRezerwacjeOnline_Dodaj_DlaModyf.',16,1);
				SET NOCOUNT OFF;
				RETURN
			END
			INSERT INTO #Wynik_XLRezerwacjeOnline_Dodaj_DlaModyf
			SELECT GUID, Ilosc FROM @DodaneRezerwacje
		END
	END

	SET NOCOUNT OFF;
	RETURN
END
</FONT>
        </PRE>
      </P>
      <HR>
    </DIV>
</BODY>
</HTML>